// Module specific build settings
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.3.0-beta4'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
    }
}

// Plugins targeting this particular module
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.android.library'

// Android configuration
android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultPublishConfig 'release'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 11
        versionName '1.1.6'
        project.archivesBaseName = 'atlantis'
    }
    buildTypes {
        release {
            minifyEnabled true
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// We want our JavaDoc to know of our dependencies
configurations {
    javadocDeps
}

dependencies {
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'org.nanohttpd:nanohttpd:2.3.0'
    compile 'com.google.guava:guava:19.0'

    javadocDeps 'com.google.code.gson:gson:2.6.2'
    javadocDeps 'org.nanohttpd:nanohttpd:2.3.0'
    javadocDeps 'com.google.guava:guava:19.0'

    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.google.code.gson:gson:2.6.2'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'junit:junit:4.12'
}

// Custom archive build configurations
apply from: 'aar.gradle'
apply from: 'jar.gradle'
apply from: 'jacoco.gradle'
apply from: 'publish.gradle'
apply from: 'bintray.gradle'
apply from: 'coveralls.gradle'
