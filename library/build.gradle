// Module specific build settings
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    }
}

// Plugins targeting this particular module
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 3
        versionName '1.0.1'
        project.archivesBaseName = 'atlantis'
        project.version = android.defaultConfig.versionName
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// We want our JavaDoc to know of our dependencies
configurations {
    javadocDeps
}

dependencies {
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'org.nanohttpd:nanohttpd:2.3.0'
    compile 'com.google.guava:guava:19.0'

    javadocDeps 'com.google.code.gson:gson:2.6.2'
    javadocDeps 'org.nanohttpd:nanohttpd:2.3.0'
    javadocDeps 'com.google.guava:guava:19.0'

    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.google.code.gson:gson:2.6.2'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'junit:junit:4.12'
}

// Custom archive build configurations
apply from: '../aar.gradle'
apply from: '../jar.gradle'
