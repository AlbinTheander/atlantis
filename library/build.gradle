// Module specific build settings
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

// Plugins targeting this particular module
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

// Android configuration
android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultPublishConfig 'release'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 7
        versionName '1.1.3'
        project.archivesBaseName = 'atlantis'
        project.version = android.defaultConfig.versionName
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// Library publication configuration
publishing {
    publications {
        atlantis(MavenPublication) {
            groupId = 'com.echsylon.atlantis'
            artifactId = 'atlantis'
            version = android.defaultConfig.versionName

            // The publication doesn't know about our dependencies for the Android gradle plugin,
            // so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the compile dependencies (we don't want the test ones), adding a
                // <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }

            // Source code bundle, from task defined in 'jar.gradle'
            artifact(sourcesJar) {
                classifier 'source'
                extension 'jar'
            }

            // JavaDoc bundle, from task defined in 'jar.gradle'
            artifact(javadocJar) {
                classifier 'javadoc'
                extension 'jar'
            }

            // The actual library, from task defined in 'jar.gradle'
            artifact(slimJar) {
                extension 'jar'
            }
        }
    }
}

// Bintray configuration
bintray {
    user = System.getenv('ATLANTIS_BINTRAY_USER')
    key = System.getenv('ATLANTIS_BINTRAY_KEY')
    publications = ['atlantis']
    publish = true

    pkg {
        repo = 'maven'
        name = 'atlantis'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/echsylon/atlantis/wiki'
        issueTrackerUrl = 'https://github.com/echsylon/atlantis/issues'
        vcsUrl = 'https://github.com/echsylon/atlantis.git'
        githubRepo = 'echsylon/atlantis'
        githubReleaseNotesFile = 'README.md'

        version {
            name = android.defaultConfig.versionName
            vcsTag = "v${android.defaultConfig.versionName}"
            released = new Date()
        }
    }
}

// We want our JavaDoc to know of our dependencies
configurations {
    javadocDeps
}

dependencies {
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'org.nanohttpd:nanohttpd:2.3.0'
    compile 'com.google.guava:guava:19.0'

    javadocDeps 'com.google.code.gson:gson:2.6.2'
    javadocDeps 'org.nanohttpd:nanohttpd:2.3.0'
    javadocDeps 'com.google.guava:guava:19.0'

    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.google.code.gson:gson:2.6.2'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'junit:junit:4.12'
}

// Custom archive build configurations
apply from: 'aar.gradle'
apply from: 'jar.gradle'
